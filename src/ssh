#!/hint/zsh

# Copyright (c) 2024, Andrea Alberti

ssh() {
  function __build_remote_window_name() {
    local remote_host_name
    local remote_window_name
    remote_window_name=""

    # Loop through all panes in the current window
    while read -r pane_id; do
      # Check if the pane has a remote host name set
      remote_host_name=$(tmux show-option -pqv -t "$pane_id" @remote-host-name)
      if [[ -n "$remote_host_name" ]]; then
        # Append the host name to the window name
        remote_window_name+="+$remote_host_name"
      fi
    done < <(tmux list-panes -F "#{pane_id}" -t "$window_id")

    # Remove leading "+" if present
    remote_window_name=${remote_window_name#+}

    # Fallback if no remote host names are found
    echo "${remote_window_name:-unknown}"
  }

  # Skip if not in a tmux session
  if [[ -z "$TMUX" ]]; then
    command ssh "$@"
    return
  fi

  # Get the current tmux window and pane IDs
  local window_id pane_id
  window_id=$(tmux display-message -p "#{window_id}")
  pane_id=$(tmux display-message -p "#{pane_id}")

  # Save the original window name if not already stored
  if [[ -z "$(tmux show-option -wqv @original-window-name)" ]]; then
    tmux set-option -wq @original-window-name "$(tmux display-message -p "#W")"
  fi

  # Perform a dry-run to get the remote host name
  local remote_host_name
  remote_host_name=$(command ssh -G "$@" 2>/dev/null | awk '/^host / {print $2}' 2>/dev/null )
  remote_host_name=${remote_host_name:-"unknown"}

  # Store the remote host name for this pane
  tmux set-option -p @remote-host-name "$remote_host_name"

  # Build the concatenated window name
  local remote_window_name
  remote_window_name=$(__build_remote_window_name)
  
  # Rename the TMUX window to the concatenated name
  tmux rename-window "$remote_window_name"
  
  # Execute the SSH command
  command ssh "$@"

  # Remove the host name from the list after SSH exits
  tmux set-option -up @remote-host-name

  # Rebuild the window name after SSH exits
  remote_window_name=$(tmux list-panes -F "#{pane_id}:#{pane_current_command}" |
    awk -F: -v wid="$window_id" '
      $1 ~ wid { hosts = hosts "+" $2 }
      END { print substr(hosts, 2) }')

  # Restore the original window name if no more active SSH sessions
  if [[ -z "$remote_window_name" ]]; then
    tmux rename-window "$(tmux show-option -wqv @original-window-name)"
    tmux set-option -uq @original-window-name
  else
    tmux rename-window "$remote_window_name"
  fi
}


